class Matrix {
  Set(int x, int y, int value): void {} 
  
  Get(int x, int y): int {}

  printMatrix(): void {
    int i;
    int j;
    for (i = 0; i < 10; i = i + 1) {
      for (j = 0; j < 10; j = j + 1) 
	   print(this.Get(i,j), "\t");
      print("\n");
    }
  }
  
  SeedMatrix(): void {
    int i;
    int j;
    for (i = 0; i < 5; i = i + 1) 
      for (j = 0; j < 5; j = j + 1) 
	   this.Set(i,j, i+j);
    this.Set(2,3,4);
    this.Set(4,6,2);
    this.Set(2,3,5);
    this.Set(0,0,1);
    this.Set(1,6,3);
    this.Set(7,7,7);
  }
}



class DenseMatrix : Matrix {
  int[][] m;
  
  construct() {
    int i;
    int j;

    this.m = array(10, int[]);
    for (i = 0; i < 10; i = i +1) 
      this.m[i] = array(10, int);

    
    for (i = 0; i < 10; i = i + 1) 
      for (j = 0; j < 10; j = j + 1) 
		this.m[i][j] = 0;
  }
  
  Set(int x, int y, int value): void {
    this.m[x][y] = value;
  }
  
  Get(int x, int y): int {
    return this.m[x][y];
  }
}


class SparseItem {
  int data;
  int y;
  SparseItem next;

  construct(int d, int y, SparseItem next) {
    this.data = d;
    this.y = y;
    this.next = next;
  }

  GetNext(): SparseItem { return this.next;}
  GetY(): int { return this.y;}
  GetData(): int { return this.data;}
  SetData(int val): int { this.data = val;}
  
}


class SparseMatrix : Matrix {
  SparseItem[] m;

  construct() {
    int i;

    
    this.m = array(10, SparseItem);
    for (i = 0; i < 10; i = i + 1)
      this.m[i] = null;

  }
  
  Find(int x, int y): SparseItem {
    SparseItem elem;
    elem = this.m[x];
    while (elem != null) {
      if (elem.GetY() == y) {
	return elem;
      }
      elem = elem.GetNext();
    }
    return null;
  }
  
  Set(int x, int y, int value): void {
    SparseItem elem;
    elem = this.Find(x,y);
    if (elem != null) {
      elem.SetData(value);
    } else {
      elem = new SparseItem(value, y, this.m[x]);
      this.m[x] = elem;
    }
  }
  
  Get(int x, int y): int {
    SparseItem elem;
    elem = this.Find(x,y);
    if (elem != null) {
      return elem.GetData();
    } else {
      return 0;
    }
  }
}

func main(): void  {
  Matrix m;
  print("Dense Rep \n");
  m = new DenseMatrix();
  m.SeedMatrix();
  m.printMatrix();
  
  
  print("Sparse Rep \n");
  m = new SparseMatrix();
  m.SeedMatrix();
  m.printMatrix();
}